BaseMgr = require('./BaseMgr')

# ---------------------------------------------------
# リサイズ管理クラス
# ---------------------------------------------------
class ResizeMgr extends BaseMgr
  
  
  # -----------------------------------------------
  # コンストラクタ
  # -----------------------------------------------
  constructor: ->
    
    super();
    
    # リサイズ時に実行させる関数を保持
    @_resizeList = [];

    # ウィンドウサイズ
    @ws = {w:0, h:0, oldW:-1, oldH:-1};
    
    # 初期化
    @_init();
  
  
  # -----------------------------------------------
  # 初期化
  # -----------------------------------------------
  _init: =>
    
    super();
    
    $(window).bind("resize", @_eResize);
    
    # ステージサイズの更新
    @_setStageSize();
  
  
  # -----------------------------------------------
  # リサイズ時に実行したい関数を登録
  # @func   : 関数 リサイズ時にウィンドウサイズを引数として渡す
  # @isCall : trueの場合この後すぐに実行される def=false
  # -----------------------------------------------
  add: (func, isCall) ->
  
    @_resizeList.push(func);
    
    if isCall? && isCall
      func(@ws.w, @ws.h);


  # -----------------------------------------------
  # リサイズ時に実行したい関数を削除
  # @func : 関数
  # -----------------------------------------------
  remove: (func) ->
  
    arr = [];
    for val, i in @_resizeList
      if val != func
        arr.push(val);
    @_resizeList = arr;
    
  
  # -----------------------------------------------
  # リサイズを外から実行
  # -----------------------------------------------
  refresh: =>
    
    @_eResize();
   
  
  # -----------------------------------------------
  # イベント リサイズ
  # -----------------------------------------------
  _eResize: (e) =>
    
    # ステージサイズの更新
    @_setStageSize();
    
    # 登録してある関数を実行
    for val, i in @_resizeList
      val(@ws.w, @ws.h);
  
  
  # -----------------------------------------------
  # ステージサイズの更新
  # -----------------------------------------------
  _setStageSize: =>
    
    if @_u.isSmt()
    
      w = window.innerWidth;
      h = window.innerHeight;
#       h = $(window).height();
      
    else
    
      if @_u.isIe8Under()
        w = $(window).width();
        h = $(window).height();
      else
        w = $(window).width();
        h = window.innerHeight;
        
    @ws.oldW = @ws.w;
    @ws.oldH = @ws.h;
    @ws.w = w;
    @ws.h = h;
  
  
  # -----------------------------------------------
  # ステージ幅
  # -----------------------------------------------
  sw: =>
    
    return @ws.w;
  
  
  # -----------------------------------------------
  # ステージ高さ
  # -----------------------------------------------
  sh: =>
    
    return @ws.h;
  



module.exports = ResizeMgr;
  